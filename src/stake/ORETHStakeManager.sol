//SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.26;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/utils/math/Math.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/token/ERC1155/extensions/ERC1155Supply.sol";

import "./PositionOptionsToken.sol";
import "../utils/Initializable.sol";
import "../utils/AutoIncrementId.sol";
import "../token/ETH/interfaces/IREY.sol";
import "../token/ETH/interfaces/IORETH.sol";
import "../token/ETH/interfaces/IOSETH.sol";
import "../blast/GasManagerable.sol";
import "./interfaces/IORETHStakeManager.sol";

/**
 * @title ORETH Stake Manager Contract
 * @dev Handles Staking of orETH
 */
contract ORETHStakeManager is IORETHStakeManager, PositionOptionsToken, Initializable, Ownable, GasManagerable, AutoIncrementId {
    using SafeERC20 for IERC20;

    uint256 public constant RATIO = 10000;
    uint256 public constant MINSTAKE = 1e16;
    uint256 public constant DAY = 24 * 3600;

    address public immutable ORETH;
    address public immutable OSETH;
    address public immutable REY;

    uint256 private _burnedYTFee;
    uint256 private _forceUnstakeFee;
    uint256 private _totalStaked;
    uint256 private _totalYieldPool;
    uint128 private _minLockupDays;
    uint128 private _maxLockupDays;

    /**
     * @param owner - Address of owner
     * @param gasManager - Address of gas manager
     * @param orETH - Address of orETH Token
     * @param osETH - Address of osETH Token
     * @param rey - Address of REY Token
     */
    constructor(
        address owner, 
        address gasManager, 
        address orETH, 
        address osETH, 
        address rey,
        string memory uri
    ) ERC1155(uri) Ownable(owner) GasManagerable(gasManager) {
        ORETH = orETH;
        OSETH = osETH;
        REY = rey;
    }


    /** view **/
    function forceUnstakeFee() external view override returns (uint256) {
        return _forceUnstakeFee;
    }

    function burnedYTFee() external view override returns (uint256) {
        return _burnedYTFee;
    }

    function totalStaked() external view override returns (uint256) {
        return _totalStaked;
    }

    function totalYieldPool() external view override returns (uint256) {
        return _totalYieldPool;
    }

    function minLockupDays() external view override returns (uint128) {
        return _minLockupDays;
    }

    function maxLockupDays() external view override returns (uint128) {
        return _maxLockupDays;
    }

    function avgStakeDays() public view override returns (uint256) {
        return IERC20(REY).totalSupply() / _totalStaked;
    }

    function calcOSETHAmount(uint256 amountInORETH, uint256 amountInREY) public view override returns (uint256) {
        return amountInORETH - (amountInREY * _totalYieldPool / IERC20(REY).totalSupply());
    }


    /** setter **/
    /**
     * @param minLockupDays_ - Min lockup days
     */
    function setMinLockupDays(uint128 minLockupDays_) public override onlyOwner {
        _minLockupDays = minLockupDays_;
        emit SetMinLockupDays(minLockupDays_);
    }

    /**
     * @param maxLockupDays_ - Max lockup days
     */
    function setMaxLockupDays(uint128 maxLockupDays_) public override onlyOwner {
        _maxLockupDays = maxLockupDays_;
        emit SetMaxLockupDays(maxLockupDays_);
    }

    /**
     * @param forceUnstakeFee_ - Force unstake fee
     */
    function setForceUnstakeFee(uint256 forceUnstakeFee_) public override onlyOwner {
        require(forceUnstakeFee_ <= RATIO, FeeOverflow());

        _forceUnstakeFee = forceUnstakeFee_;
        emit SetForceUnstakeFee(forceUnstakeFee_);
    }

    /**
     * @param burnedYTFee_ - Burn more YT when force unstake
     */
    function setBurnedYTFee(uint256 burnedYTFee_) public override onlyOwner {
        require(burnedYTFee_ <= RATIO, FeeOverflow());

        _burnedYTFee = burnedYTFee_;
        emit SetBurnedYTFee(burnedYTFee_);
    }

    
    /** function **/
    /**
     * @dev Initializer
     * @param forceUnstakeFee_ - Force unstake fee
     * @param burnedYTFee_ - Burn more YT when force unstake
     * @param minLockupDays_ - Min lockup days
     * @param maxLockupDays_ - Max lockup days
     */
    function initialize(
        uint256 forceUnstakeFee_, 
        uint256 burnedYTFee_,
        uint128 minLockupDays_, 
        uint128 maxLockupDays_
    ) external override initializer {
        setForceUnstakeFee(forceUnstakeFee_);
        setBurnedYTFee(burnedYTFee_);
        setMinLockupDays(minLockupDays_);
        setMaxLockupDays(maxLockupDays_);
    }

    /**
     * @dev Allows user to deposit orETH, then mints osETH and REY for the user.
     * @param amountInORETH - orETH staked amount
     * @param lockupDays - User can withdraw after lockupDays
     * @param positionOwner - Owner of position
     * @param osETHTo - Receiver of osETH
     * @param reyTo - Receiver of REY
     * @notice User must have approved this contract to spend orETH
     */
    function stake(
        uint128 amountInORETH, 
        uint256 lockupDays, 
        address positionOwner, 
        address osETHTo, 
        address reyTo
    ) external override returns (uint256 amountInOSETH, uint256 amountInREY) {
        require(amountInORETH >= MINSTAKE, MinStakeInsufficient(MINSTAKE));
        require(
            lockupDays >= _minLockupDays && lockupDays <= _maxLockupDays, 
            InvalidLockupDays(_minLockupDays, _maxLockupDays)
        );

        address msgSender = msg.sender;
        uint256 deadline;
        unchecked {
            _totalStaked += amountInORETH;
            deadline = block.timestamp + lockupDays * DAY;
            amountInREY = amountInORETH * lockupDays;
        }

        IREY(REY).mint(reyTo, amountInREY);
        amountInOSETH = calcOSETHAmount(amountInORETH, amountInREY);
        uint256 positionId = _nextId();
        positions[positionId] = Position(ORETH, amountInORETH, uint128(amountInOSETH), deadline);

        _mint(positionOwner, positionId, amountInORETH, "");
        IERC20(ORETH).safeTransferFrom(msgSender, address(this), amountInORETH);
        IOSETH(OSETH).mint(osETHTo, amountInOSETH);

        emit StakeORETH(positionId, amountInORETH, amountInOSETH, amountInREY, deadline);
    }

    /**
     * @dev Allows user to unstake funds. If force unstake, need to pay force unstake fee.
     * @param positionId - Staked ETH Position Id
     * @param share - Share of the position
     */
    function unstake(uint256 positionId, uint256 share) external override {
        address msgSender = msg.sender;
        burn(msgSender, positionId, share);
        
        Position storage position = positions[positionId];
        uint256 stakedAmount = position.stakedAmount;
        uint256 PTAmount = position.PTAmount;
        uint256 deadline = position.deadline;
        uint256 burnedOSETH = Math.mulDiv(PTAmount, share, stakedAmount, Math.Rounding.Ceil);
        IOSETH(OSETH).burn(msgSender, burnedOSETH);
        unchecked {
            _totalStaked -= share;
        }
        
        uint256 burnedREY;
        uint256 currentTime = block.timestamp;
        if (deadline > currentTime) {
            unchecked {
                burnedREY = share * Math.ceilDiv(deadline - currentTime, DAY) * (RATIO + _burnedYTFee) / RATIO;
            }
            IREY(REY).burn(msgSender, burnedREY);
            position.deadline = currentTime;

            uint256 fee;
            unchecked {
                fee = share * _forceUnstakeFee / RATIO;
                share -= fee;
            }
            IORETH(ORETH).withdraw(fee);
            Address.sendValue(payable(IORETH(ORETH).revenuePool()), fee);
        }        
        IERC20(ORETH).safeTransfer(msgSender, share);

        emit Unstake(positionId, share, burnedOSETH, burnedREY);
    }

    /**
     * @dev Allows user burn REY to withdraw yield
     * @param burnedREY - Amount of burned REY
     */
    function withdrawYield(uint256 burnedREY) external override returns (uint256 yieldAmount) {
        require(burnedREY != 0, ZeroInput());

        unchecked {
            yieldAmount = _totalYieldPool * burnedREY / IREY(REY).totalSupply();
            _totalYieldPool -= yieldAmount;
        }

        address msgSender = msg.sender;
        IREY(REY).burn(msgSender, burnedREY);
        IERC20(ORETH).safeTransfer(msgSender, yieldAmount);

        emit WithdrawYield(msgSender, burnedREY, yieldAmount);
    }

    /**
     * @dev Accumulate the native yield
     * @param nativeYield - Additional native yield amount
     */
    function accumYieldPool(uint256 nativeYield) external override {
        require(msg.sender == ORETH, PermissionDenied());

        unchecked {
            _totalYieldPool += nativeYield;
        }
    }
}
